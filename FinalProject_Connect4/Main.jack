class Main
{
	// run the homepage
	function void main()
	{
        var ConnectGame game;
		var int menuOption;
		
		let menuOption = 0;
		do Main.drawHomepage();
		
		// menu operations
		while ( ~( (Keyboard.keyPressed() = 128)  & (menuOption = 2) ) ) // exit the game
		{
			if ((Keyboard.keyPressed() = 128) & ( (menuOption = 0) | (menuOption = 1) ))
			{
				let game = ConnectGame.new();
				do game.play(menuOption);
				do game.clear();
				do Sys.wait(300);
				
				let menuOption = 0;
				do Screen.clearScreen();
				do Main.drawHomepage();
			}

			if ((Keyboard.keyPressed() = 132) & (menuOption < 2))
			{
				let menuOption = menuOption + 1;
				do Main.indicator(menuOption);
				do Sys.wait(50);
			}

			if ((Keyboard.keyPressed() = 130) & (menuOption > 0))
			{
				let menuOption = menuOption - 1;
				do Main.indicator(menuOption);
				do Sys.wait(50);
			}
			
			do Sys.wait(50);
		}
		
		return;
	}
	
	function void drawHomepage()
	{
		// draw the game's logo.
		do Screen.clearScreen();
		do Output.moveCursor(3, 28);
		do Output.printString("Connect 4");
		do Screen.setColor(true);
		do Screen.drawCircle(192, 60, 5);
		do Screen.drawCircle(227, 66, 5);
		do Screen.drawCircle(270, 63, 5);
		do Screen.drawCircle(311, 59, 5);
		do Screen.drawLine(192, 60, 227, 66);
		do Screen.drawLine(227, 66, 270, 63);
		do Screen.drawLine(270, 63, 311, 59);
		

		// show target & instructions
		do Output.moveCursor(7, 29);
		do Output.printString("Target");
		do Output.moveCursor(9, 6);
		do Output.printString("Form a horizontal, vertical or diagonal line of 4 discs");
		do Output.moveCursor(10, 12);
		do Output.printString("before your opponent does so; then you WIN!!");
		
		do Output.moveCursor(12, 26);
		do Output.printString("How to Play");
		do Output.moveCursor(14, 6);
		do Output.printString("Use LEFT & RIGHT arrow key to switch between columns;");
		do Output.moveCursor(15, 11);
		do Output.printString("then, press DOWN arrow key to place your disc.");
		do Output.moveCursor(16, 5);
		do Output.printString("(Or, remove your disc from the bottom in Pop-Out mode.)");
		

		
		// show menu options.
		do Output.moveCursor(20, 15);
		do Output.printString("Classic");
		do Output.moveCursor(20, 29);
		do Output.printString("Pop-Out");
		do Output.moveCursor(20, 44);
		do Output.printString("Quit");
		do Main.indicator(0);
		
		return;
	}
	
	// tell user the current menu selection
	function void indicator(int op)
	{	
		if (op = 0)
		{
			do Output.moveCursor(20, 12);
			do Output.printString(">>");
			do Output.moveCursor(20, 26);
			do Output.printString("  ");
			do Output.moveCursor(20, 41);
			do Output.printString("  ");
		}
		if (op = 1)
		{
			do Output.moveCursor(20, 12);
			do Output.printString("  ");
			do Output.moveCursor(20, 26);
			do Output.printString(">>");
			do Output.moveCursor(20, 41);
			do Output.printString("  ");
		}
		if (op = 2)
		{
			do Output.moveCursor(20, 12);
			do Output.printString("  ");
			do Output.moveCursor(20, 26);
			do Output.printString("  ");
			do Output.moveCursor(20, 41);
			do Output.printString(">>");
		}

		return;
	}	
}