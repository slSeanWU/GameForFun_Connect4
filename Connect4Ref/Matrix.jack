/* 	This is part of a Connect 4 game made for educational purposes
	as Project 09 of the Nand2Tetris course.
	
	Written by Slayorious */
	
/** 
	Implements a matrix for use as a 2 dimensional array.
*/

class Matrix
{
	field Array columns;
	field int width;
	field int height;
	
	// Initializes all values in the matrix to 0.
	constructor Matrix new(int w, int h)
	{
		var int i, j;
		var Array row;
		let i = 0;
		let width = w;
		let height = h;
		let columns = Array.new(w);
		while (i < width)
		{
			let j = 0;
			let columns[i] = Array.new(h);
			let row = columns[i];
			while (j < height)
			{
				let row[j] = 0;
				let j = j + 1;
			}
			let i = i + 1;
		}
		
		return this;
	}
	
	method void set(int x, int y, int value)
	{
		var Array row;
		let row = columns[x];
		let row[y]= value;
		
		return;
	}
	
	method int get(int x, int y)
	{
		var Array row;
		let row = columns[x];
		
		return row[y];
	}
	
	method void dispose()
	{
		var int i;
		var Array row;
		let i = 0;
		while (i < width)
		{
			let row = columns[i];
			do row.dispose();
			let i = i + 1;
		}
		do columns.dispose();
		do Memory.deAlloc(this);
		return;
	}
}
